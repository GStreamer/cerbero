From d8eee5ae5b52e625426a5d8b830d0c935f91f788 Mon Sep 17 00:00:00 2001
From: "L. E. Segovia" <amy@centricular.com>
Date: Thu, 24 Apr 2025 13:11:50 -0300
Subject: [PATCH 1/5] Add Meson build

---
 interface/meson.build | 23 +++++++++++++++++++
 meson.build           | 53 +++++++++++++++++++++++++++++++++++++++++++
 paranoia/meson.build  | 17 ++++++++++++++
 3 files changed, 93 insertions(+)
 create mode 100644 interface/meson.build
 create mode 100644 meson.build
 create mode 100644 paranoia/meson.build

diff --git a/interface/meson.build b/interface/meson.build
new file mode 100644
index 0000000..9113b25
--- /dev/null
+++ b/interface/meson.build
@@ -0,0 +1,23 @@
+interface_args = []
+
+if get_option('optimization') == '0'
+    interface_args += ['-DCDDA_TEST']
+endif
+
+cdda_interface = library(
+    'cdda_interface',
+    files(
+        'scan_devices.c',
+        'common_interface.c',
+        'cooked_interface.c',
+        'interface.c',
+        'scsi_interface.c',
+        'smallft.c',
+        'toc.c',
+        'test_interface.c',
+    ),
+    c_args: project_args + interface_args,
+    dependencies: [m_dep, rt_dep],
+    pic: true,
+    version: '0.@0@'.format(meson.project_version())
+)
diff --git a/meson.build b/meson.build
new file mode 100644
index 0000000..ed3599d
--- /dev/null
+++ b/meson.build
@@ -0,0 +1,53 @@
+project('cdparanoia', 'c',
+    version: '10.2',
+    default_options: [
+        'c_std=gnu99,c11',
+        'buildtype=debugoptimized',
+    ],
+    meson_version: '>= 1.3.0'
+)
+
+cc = meson.get_compiler('c')
+
+project_args = cc.get_supported_arguments(
+    ['-fsigned-char']
+)
+
+if host_machine.cpu_family() == 'x86' and get_option('optimization') != '0'
+    project_args += cc.get_supported_arguments(
+        ['-ffast-math', '-finline-functions']
+    )
+endif
+
+if cc.has_header('linux/sbpcd.h')
+    project_args += ['-DSBPCD_H=1']
+endif
+
+if cc.has_header('linux/ucdrom.h')
+    project_args += ['-DUCDROM_H=1']
+endif
+
+m_dep = cc.find_library('m', required: false)
+rt_dep = cc.find_library('rt', required: false)
+
+subdir('interface')
+subdir('paranoia')
+
+cdparanoia = executable(
+    'cdparanoia',
+    files(
+        'main.c',
+        'report.c',
+        'header.c',
+        'buffering_write.c',
+        'cachetest.c',
+    ),
+    link_with: [cdda_interface, cdda_paranoia],
+    install: true,
+)
+
+install_man(
+    files(
+        'cdparanoia.1',
+    ),
+)
diff --git a/paranoia/meson.build b/paranoia/meson.build
new file mode 100644
index 0000000..d48556a
--- /dev/null
+++ b/paranoia/meson.build
@@ -0,0 +1,17 @@
+paranoia_args = []
+
+cdda_paranoia = library(
+    'cdda_paranoia',
+    files(
+        'paranoia.c',
+        'p_block.c',
+        'overlap.c',
+        'gap.c',
+        'isort.c',
+    ),
+    c_args: project_args + paranoia_args,
+    link_with: cdda_interface,
+    dependencies: [m_dep],
+    pic: true,
+    version: '0.@0@'.format(meson.project_version())
+)
-- 
2.39.5 (Apple Git-154)


From 071e05d3fa39742b073ef141fabbcdfdd366e4a5 Mon Sep 17 00:00:00 2001
From: "L. E. Segovia" <amy@centricular.com>
Date: Thu, 24 Apr 2025 13:28:50 -0300
Subject: [PATCH 2/5] meson: Ban more warnings

---
 meson.build | 18 +++++++++++++++---
 1 file changed, 15 insertions(+), 3 deletions(-)

diff --git a/meson.build b/meson.build
index ed3599d..3d31397 100644
--- a/meson.build
+++ b/meson.build
@@ -9,9 +9,21 @@ project('cdparanoia', 'c',
 
 cc = meson.get_compiler('c')
 
-project_args = cc.get_supported_arguments(
-    ['-fsigned-char']
-)
+project_args = cc.get_supported_arguments([
+    '-fsigned-char',
+    # Warnings from recent GCC
+    '-Wno-unused-but-set-variable',
+    '-Wno-misleading-indentation',
+    '-Wno-maybe-uninitialized',
+    '-Wno-unused-function',
+    '-Wno-parentheses',
+    '-Wno-sizeof-pointer-memaccess',
+    '-Wno-unused-variable',
+    '-Wno-pointer-sign',
+    '-Wno-address',
+    # Warnings from recent clang
+    '-Wno-absolute-value'
+])
 
 if host_machine.cpu_family() == 'x86' and get_option('optimization') != '0'
     project_args += cc.get_supported_arguments(
-- 
2.39.5 (Apple Git-154)


From c55fb98d89bf48da991a47ce6a6992f35068d4f7 Mon Sep 17 00:00:00 2001
From: "L. E. Segovia" <amy@centricular.com>
Date: Thu, 24 Apr 2025 13:31:18 -0300
Subject: [PATCH 3/5] meson: Add missing libraries

---
 meson.build | 1 +
 1 file changed, 1 insertion(+)

diff --git a/meson.build b/meson.build
index 3d31397..2e75563 100644
--- a/meson.build
+++ b/meson.build
@@ -55,6 +55,7 @@ cdparanoia = executable(
         'cachetest.c',
     ),
     link_with: [cdda_interface, cdda_paranoia],
+    dependencies: [m_dep, rt_dep],
     install: true,
 )
 
-- 
2.39.5 (Apple Git-154)


From 23e9e27fef3123cb3236cbfa8ced898ee0bb82f4 Mon Sep 17 00:00:00 2001
From: "L. E. Segovia" <amy@centricular.com>
Date: Thu, 24 Apr 2025 13:50:08 -0300
Subject: [PATCH 4/5] meson: Add install for headers

---
 interface/meson.build | 6 ++++++
 meson.build           | 6 ++++++
 paranoia/meson.build  | 6 ++++++
 3 files changed, 18 insertions(+)

diff --git a/interface/meson.build b/interface/meson.build
index 9113b25..90a9df7 100644
--- a/interface/meson.build
+++ b/interface/meson.build
@@ -21,3 +21,9 @@ cdda_interface = library(
     pic: true,
     version: '0.@0@'.format(meson.project_version())
 )
+
+install_headers(
+    files(
+        'cdda_interface.h'
+    )
+)
diff --git a/meson.build b/meson.build
index 2e75563..2c78aea 100644
--- a/meson.build
+++ b/meson.build
@@ -64,3 +64,9 @@ install_man(
         'cdparanoia.1',
     ),
 )
+
+install_headers(
+    files(
+        'utils.h'
+    )
+)
diff --git a/paranoia/meson.build b/paranoia/meson.build
index d48556a..c2758b8 100644
--- a/paranoia/meson.build
+++ b/paranoia/meson.build
@@ -15,3 +15,9 @@ cdda_paranoia = library(
     pic: true,
     version: '0.@0@'.format(meson.project_version())
 )
+
+install_headers(
+    files(
+        'cdda_paranoia.h'
+    )
+)
-- 
2.39.5 (Apple Git-154)


From e063d289d98408e6b8fee23cd3e8e4b6e788f0af Mon Sep 17 00:00:00 2001
From: "L. E. Segovia" <amy@centricular.com>
Date: Tue, 29 Apr 2025 22:56:33 -0300
Subject: [PATCH 5/5] Add missing install

---
 interface/meson.build | 3 ++-
 paranoia/meson.build  | 3 ++-
 2 files changed, 4 insertions(+), 2 deletions(-)

diff --git a/interface/meson.build b/interface/meson.build
index 90a9df7..5575981 100644
--- a/interface/meson.build
+++ b/interface/meson.build
@@ -19,7 +19,8 @@ cdda_interface = library(
     c_args: project_args + interface_args,
     dependencies: [m_dep, rt_dep],
     pic: true,
-    version: '0.@0@'.format(meson.project_version())
+    version: '0.@0@'.format(meson.project_version()),
+    install: true
 )
 
 install_headers(
diff --git a/paranoia/meson.build b/paranoia/meson.build
index c2758b8..b075839 100644
--- a/paranoia/meson.build
+++ b/paranoia/meson.build
@@ -13,7 +13,8 @@ cdda_paranoia = library(
     link_with: cdda_interface,
     dependencies: [m_dep],
     pic: true,
-    version: '0.@0@'.format(meson.project_version())
+    version: '0.@0@'.format(meson.project_version()),
+    install: true
 )
 
 install_headers(
-- 
2.39.5 (Apple Git-154)

